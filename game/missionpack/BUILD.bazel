load("//build_defs:rules.bzl", "CLIENT_GAME_LINKOPTS", "SERVER_GAME_LINKOPTS", "UI_LINKOPTS")

# Visible to all the packages that produce distributions
_DIST_VISIBILITY = [
    "//game:__pkg__",
    "//game/mac:__pkg__",
]

cc_binary(
    name = "cgame.so",
    linkopts = CLIENT_GAME_LINKOPTS,
    linkshared = 1,
    visibility = _DIST_VISIBILITY,
    deps = [
        "//build_defs:exported_symbols.lds",
        "//code/cgame:teamarena",
    ],
)

cc_binary(
    name = "qagame.so",
    linkopts = SERVER_GAME_LINKOPTS,
    linkshared = 1,
    visibility = _DIST_VISIBILITY,
    deps = [
        "//build_defs:exported_symbols.lds",
        "//code/game:teamarena",
    ],
)

cc_binary(
    name = "ui.so",
    linkopts = UI_LINKOPTS,
    linkshared = 1,
    visibility = _DIST_VISIBILITY,
    deps = [
        "//build_defs:exported_symbols.lds",
        "//code/ui",
    ],
)

genrule(
    name = "pak0",
    srcs = [
        "@q3a_missionpack_pak0//:files",
        "@q3a_missionpack_pak1//:files",
        "@q3a_missionpack_pak3//:files",
        "//content/teamarena:ui_assets",
        "@QuakeArenaArcade_missionpack//:files",
    ],
    outs = ["pak0.pk3"],
    cmd = """
rm -rf pak
$(location //build_defs:copy_files.sh) external/QuakeArenaArcade_missionpack/ pak $(locations @QuakeArenaArcade_missionpack//:files)
$(location //build_defs:copy_files.sh) external/q3a_missionpack_pak0/ pak $(locations @q3a_missionpack_pak0//:files)
$(location //build_defs:copy_files.sh) external/q3a_missionpack_pak1/ pak $(locations @q3a_missionpack_pak1//:files)
$(location //build_defs:copy_files.sh) external/q3a_missionpack_pak3/ pak $(locations @q3a_missionpack_pak3//:files)
$(location //build_defs:copy_files.sh) content/teamarena/ pak $(locations //content/teamarena:ui_assets)
cd pak
find .  ! -type d |  sed -e "s/^\\.\\///" | awk '{print "\\"" $$0 "\\"" }' | sort | xargs ../$(location @bazel_tools//tools/zip:zipper) c ../$@
    """,
    tools = [
        "//build_defs:copy_files.sh",
        "@bazel_tools//tools/zip:zipper",
    ],
    visibility = _DIST_VISIBILITY,
)
