load("@build_bazel_rules_apple//apple:macos.bzl", "macos_application")
load("@build_bazel_rules_apple//apple:versioning.bzl", "apple_bundle_version")

# A version scheme that parses version information out of the build
# label and uses a fallback for developers' builds. For example, the
# following command
#
#    bazel build //game/mac:Quake3Arena.zip --embed_label=Quake3Arena_1.2_build_345
#
# would yield the Info.plist values:
#
#    CFBundleVersion = "1.2.345"
#    CFBundleShortVersionString = "1.2"
#
# and the development builds using the command:
#
#    bazel build //game/mac:Quake3Arena.zip
#
# would yield the values:
#
#    CFBundleVersion = "99.99.99"
#    CFBundleShortVersionString = "99.99"
#
apple_bundle_version(
    name = "build_label_version",
    build_label_pattern = "Quake3Arena_{version}_build_{build}",
    build_version = "{version}.{build}",
    capture_groups = {
        "version": "\\d+\\.\\d+",
        "build": "\\d+",
    },
    fallback_build_label = "Quake3Arena_99.99_build_99",
    short_version_string = "{version}",
)

# This target is a WIP and as we do not currently use it, the work has stopped on it
# The application *should* bundle the SDL framework or maybe we should inline the SDL
# source into our build (and perhaps curl?). Then when building it we would need to run
#
#   $ bazel build //game/mac:Quake3Arena.zip --embed_label=Quake3Arena_1.2_build_345
macos_application(
    name = "Quake3Arena",
    additional_contents = {
        "//game:Quake3ArenaServer": "MacOS",
        "//code/renderergl2:renderer_opengl2.so": "MacOS",
        # TODO: All these libraries should be suffixed with the arch...i.e "arm64"
        "//game/baseq3:cgame.so": "MacOS/baseq3",
        "//game/baseq3:qagame.so": "MacOS/baseq3",
        "//game/baseq3:ui.so": "MacOS/baseq3",
        "//game/teamarena:cgame.so": "MacOS/missionpack",
        "//game/teamarena:qagame.so": "MacOS/missionpack",
        "//game/teamarena:ui.so": "MacOS/missionpack",

        # Application Icons
        ":quake3_flat.icns": "Resources",

        # Data files can not be below MacOS directory as the code signing process will expect it to be
        # code and thus must have a signature. Which it obviously does not. This places it at a different
        # location in which arbitrary resources can be stored.
        # See https://developer.apple.com/library/archive/technotes/tn2206/_index.html for further details.
        "//game/baseq3:pak0.pk3": "Resources/baseq3",
        "//game/baseq3:pak9hqq36.pk3": "Resources/baseq3",
        "//game/teamarena:pak0.pk3": "Resources/missionpack",
    },
    # TODO: Name this bundle id according to final product name
    bundle_id = "org.realityforge.Quake3Arena",
    # allows for unsigned executable memory in hardened runtime
    entitlements = ":Quake3Arena.entitlements",
    infoplists = [":Info.plist"],
    minimum_os_version = "10.11",
    tags = ["manual"],
    version = ":build_label_version",
    deps = ["//code/client:clientexe"],
)
